# Forked from https://github.com/lkundrak/perl-Log-Journald,
# as updates there seem to have stopped
pkgname=$(basename $(pwd))
pkgver=0.31
pkgrel=8
pkgdesc='Log to systemd from Perl'
_dist=Log-Journald
arch=('any')
url="https://github.com/uboslinux/ubos-perl"
license=('GPL' 'PerlArtistic')
depends=('perl')
makedepends=('perl-module-build')
options=('!emptydirs' purge)

build() (
  cd "$startdir/code"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 MODULEBUILDRC=/dev/null
  [[ -e ./Build ]] && ./Build clean
  /usr/bin/perl Build.PL
  ./Build
)

check() (
  cd "$startdir/code"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1
  ./Build test
)

package() (
  cd "$startdir/code"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  ./Build install installdirs=vendor destdir="$pkgdir"
)
